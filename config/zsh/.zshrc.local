# .zshrc.local

# --------------------------------------------------------------------- INCLUDES
if test -d "${ZDOTDIR:-${HOME}}/zsh.d"; then
	for file in "${ZDOTDIR:-${HOME}}/zsh.d"/*; do
		xsource "${file}"
	done
	unset file
fi

# ---------------------------------------------------------------------- OPTIONS
setopt histallowclobber
setopt histexpiredupsfirst
setopt histfindnodups
setopt histignorealldups
setopt histignoredups
setopt histsavenodups
setopt nonomatch
setopt nosharehistory

# ---------------------------------------------------------------------- WIDGETS
list-dir() { echo; exa -a; echo; echo; zle redisplay }
zle -N list-dir
bindkey "^[l" "list-dir"

repeat-last-cmd() { BUFFER=$(history -n -1); zle clear-screen; zle accept-line }
zle -N repeat-last-cmd
bindkey "^[r" "repeat-last-cmd"

yank-buffer() { echo $BUFFER | copy }
zle -N yank-buffer
bindkey "^X^Y" "yank-buffer"

rationalise-dot() {
local MATCH
if [[ $LBUFFER =~ '(^|/| |	|'$'\n''|\||;|&)\.\.$' ]]; then
 LBUFFER+=/
 zle self-insert
 zle self-insert
else
 zle self-insert
fi
}
zle -N rationalise-dot
bindkey . rationalise-dot
bindkey -M isearch . self-insert # without this, typing a . aborts incremental history search

# ------------------------------------------------------------------------- MISC
eval "$(zoxide init zsh)"
alias zedit='FZF_DEFAULT_OPTS=${FZF_DEFAULT_OPTS/--height 25%/} zoxide edit'

# use the vi navigation keys (hjkl) besides cursor keys in menu completion
bindkey -M menuselect 'h' vi-backward-char        # left
bindkey -M menuselect 'k' vi-up-line-or-history   # up
bindkey -M menuselect 'l' vi-forward-char         # right
bindkey -M menuselect 'j' vi-down-line-or-history # bottom

# expand shell aliases
zstyle ':completion:*' completer _expand_alias _complete _approximate

# # set command prediction from history, see 'man 1 zshcontrib'
# zrcautoload predict-on
# zle -N predict-on
# zle -N predict-off

# ---------------------------------------------------------------------- PLUGINS
xsource "${XDG_DATA_HOME}/zsh/plugins/fsh/fast-syntax-highlighting.plugin.zsh"

# ----------------------------------------------------------------------- PROMPT
prompt off
export STARSHIP_CONFIG=$HOME/.config/starship/config.toml
eval "$(starship init zsh)"
